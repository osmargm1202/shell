pragma ComponentBehavior: Bound

import QtQuick.Layouts
import Caelestia.Config
import qs.modules.nexus.common

PageBase {
    id: root

    title: qsTr("Dock")
    isSubPage: true

    ColumnLayout {
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.top: parent.top
        width: root.cappedWidth
        spacing: Tokens.spacing.extraSmall / 2

        ToggleRow {
            Layout.fillWidth: true
            first: true
            text: qsTr("Enable component")
            checked: {
                for (let i = 0; i < Config.bar.entries.length; i++) {
                    if (Config.bar.entries[i].id === "dock")
                        return Config.bar.entries[i].enabled;
                }
                return false;
            }
            onToggled: {
                let currentEntries = GlobalConfig.bar.entries;
                let newEntries = [
                    { "id": "logo", "enabled": true },
                    { "id": "workspaces", "enabled": true },
                    { "id": "spacer", "enabled": true },
                    { "id": "activeWindow", "enabled": true },
                    { "id": "dock", "enabled": false },
                    { "id": "spacer", "enabled": true },
                    { "id": "tray", "enabled": true },
                    { "id": "github", "enabled": true },
                    { "id": "clock", "enabled": true },
                    { "id": "statusIcons", "enabled": true },
                    { "id": "power", "enabled": true }
                ];
                for (let i = 0; i < newEntries.length; i++) {
                    if (newEntries[i].id === "dock") {
                        newEntries[i].enabled = checked;
                    } else if (newEntries[i].id !== "spacer") {
                        let existing = currentEntries.find(e => e.id === newEntries[i].id);
                        if (existing !== undefined) newEntries[i].enabled = existing.enabled;
                    }
                }
                GlobalConfig.bar.entries = newEntries;
            }
        }

        ToggleRow {
            Layout.fillWidth: true
            text: qsTr("Monitor center")
            subtext: qsTr("Center the dock relative to the physical monitor")
            checked: Config.bar.dock.monitorCenter
            onToggled: GlobalConfig.bar.dock.monitorCenter = checked
        }

        ToggleRow {
            Layout.fillWidth: true
            text: qsTr("Recolour icons")
            subtext: qsTr("Recolour application icons using the system theme")
            checked: Config.bar.dock.recolourIcons
            onToggled: GlobalConfig.bar.dock.recolourIcons = checked
        }

        StepperRow {
            Layout.fillWidth: true
            label: qsTr("Hover hide delay")
            subtext: qsTr("How long the app preview stays open after leaving the dock icon (ms)")
            value: Config.bar.dock.hoverHideDelay
            from: 0
            to: 1000
            stepSize: 50
            onMoved: v => GlobalConfig.bar.dock.hoverHideDelay = v
        }

        StepperRow {
            Layout.fillWidth: true
            last: true
            label: qsTr("Icon magnification on hover")
            subtext: qsTr("How much dock icons grow when hovered (%)")
            value: Math.round(Config.bar.dock.hoverIconScale * 100)
            from: 100
            to: 200
            stepSize: 5
            onMoved: v => GlobalConfig.bar.dock.hoverIconScale = v / 100
        }
    }
}
